plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

ext {
    libraryName = 'JavaSound Framework Core'
    libraryGroupName = 'com.tianscar.javasound'
    libraryVendorName = 'Tianscar'

    libraryVersionCode = 1
    libraryVersionName = '1.0.0'

    librarySourceCompatibility = JavaVersion.VERSION_1_8
    libraryTargetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = librarySourceCompatibility
targetCompatibility = libraryTargetCompatibility

group = libraryGroupName
version = libraryVersionName

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libraryGroupName
            artifactId = rootProject.name
            version = libraryVersionName
            from components.java
            pom {
                name = libraryName
                description = 'The stand-alone JavaSound framework core ported from JDK'
                url = 'https://github.com/Tianscar/javasound-core'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2, with the Classpath Exception'
                        url = 'https://openjdk.org/legal/gplv2+ce.html'
                    }
                }
                developers {
                    developer {
                        id = 'Tianscar'
                        name = 'Karstian Lee'
                        email = 'tianscar@protonmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com/Tianscar/javasound-core.git'
                    developerConnection = 'scm:git:git@github.com/Tianscar/javasound-core.git'
                    url = 'https://github.com/Tianscar/javasound-core'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.addStringOption("Xdoclint:none", "-quiet") // TODO cleanup JavaDocs
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
